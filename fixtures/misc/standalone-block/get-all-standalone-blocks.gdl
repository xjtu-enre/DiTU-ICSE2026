// script

use coref::javascript::*

/**
 * FIXME:
 * ant-design@6033c69e600fde6f999d6b9e1497b07d03530966
 *   + components/select/index.tsx:94:6 Arrow function's function body's node's parent was SourceFile
 *   + components/tree-select/index.tsx:125:6
 */

fn generalStandaloneBlock(
    filePath: string,
    blockOid: int,
    blockStartLine: int,
    blockStartColumn: int,
    blockEndLine: int,
    blockEndColumn: int,
    hasDeclaration_SB: string,
    parentNodeType: string,
) -> bool {
    let (db = getDefaultDB()) {
        for (block in BlockStatement(db)) {
            if (isStandaloneBlock(block) &&
                filePath = block.getRelativePath() &&
                blockOid = block.id &&
                blockStartLine = block.getStartLineNumber() &&
                blockStartColumn = block.getStartColumnNumber() &&
                blockEndLine = block.getEndLineNumber() &&
                blockEndColumn = block.getEndColumnNumber() &&
                parentNodeType = getNodeTypeString(block.getParent())
            ) {
                if (hasDeclarationChild(block) &&
                    hasDeclaration_SB = "true"
                ) {
                    return true
                }

                if (!hasDeclarationChild(block) &&
                    hasDeclaration_SB = "false"
                ) {
                    return true
                }
            }
        }
    }
}

fn getAStandaloneBlockInCaseClause(node: CaseClause) -> *Statement {
    for (stmt in node.getAStatement()) {
        if (isBlockStatement(stmt.to<Node>())) {
            yield stmt
        }
    }
}

fn getAStandaloneBlockInDefaultClause(node: DefaultClause) -> *Statement {
    for (stmt in node.getAStatement()) {
        if (isBlockStatement(stmt.to<Node>())) {
            yield stmt
        }
    }
}

fn switchCaseClauseStandaloneBlock(
    filePath: string,
    switchStatementOid: int,
    caseClauseOid: int,
    caseClauseStartLine: int,
    caseClauseStartColumn: int,
    blockCount: int,
) -> bool {
    let (db = getDefaultDB()) {
        for (case in CaseClause(db)) {
            if (filePath = case.getRelativePath() &&
                //                        v SwitchStatement.caseBlock
                switchStatementOid = case.getParent().getParent().id &&
                caseClauseOid = case.id &&
                caseClauseStartLine = case.getStartLineNumber() &&
                caseClauseStartColumn = case.getStartColumnNumber() &&
                blockCount = getAStandaloneBlockInCaseClause(case).len()
            ) {
                return true
            }
        }

        for (case in DefaultClause(db)) {
            if (filePath = case.getRelativePath() &&
                //                        v SwitchStatement.caseBlock
                switchStatementOid = case.getParent().getParent().id &&
                caseClauseOid = case.id &&
                caseClauseStartLine = case.getStartLineNumber() &&
                caseClauseStartColumn = case.getStartColumnNumber() &&
                blockCount = getAStandaloneBlockInDefaultClause(case).len()
            ) {
                return true
            }
        }
    }
}

fn main() {
    output(generalStandaloneBlock())
    output(switchCaseClauseStandaloneBlock())
}
